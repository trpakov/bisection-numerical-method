// Изчистване на конзолата и на заредените в паметта променливи.
clc; clear;

// Показване на стойност в конзолата, в случай, че не сме поставили ";" в края.
mode(0);

// Дефиниране на променливи.
a = -1; // Лява граница на интервала.
b = -0.5; // Дясна граница на интервала. 
err = 0.0001;// Грешка (целева точност).
iter = 0; // Брояч.

// Дефиниране на функцията f(x).
function z=f(x)
    z = x^4-4*x^3+4*x^2+x-2;
endfunction

fa = f(a); // Стойност на ф-ята в левия край на интервала.

// Цикъл на метода.
while b-a > err // Докато текущия интервал е по-голям от грешката.
    iter = iter+1; // Инкрементиране на брояча.
    mid = (a+b)/2; // Среда на интервала.
    fmid = f(mid); // Стойността на ф-ята в средата на интервала.
    
    // Ако ф-ята има различни знаци в левия край и в средата на интервала:
    if fa * fmid < 0
        b = mid; // Коренът се намира в левия подинтервал. Затова променяме дясната граница на средата на интервала.
    else // В противен случай:
        a = mid; // Коренът се намира в десния подинтервал. Затова променяме лявата граница на средата на интервала.
        fa = fmid; //Записваме в променливата fa, участваща в проверката, стойността на функцията в новата лява граница.
    end
end

// След достигане до необходимата точност намираме средата на последния интервал, което е и търсеното от нас решение.
x=(a+b)/2
iter // Не слагаме ";" в края, за да изведем на конзолата отговора и броя итерации.



